import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "com.github.hierynomus.license" version "0.11.0"
    id "java"
    id "maven-publish"
    id "wrapper"
}

sourceCompatibility = "1.7"

defaultTasks 'clean', 'licenseFormat', 'check', 'build'

group = "com.tealcube.minecraft.bukkit"
archivesBaseName = "strife"
version = "1.0.0-SNAPSHOT"

repositories {
    mavenCentral()
    maven {
        url "http://tealcube.com/nexus/content/groups/public/"
    }
    maven {
        url "https://hub.spigotmc.org/nexus/content/groups/public/"
    }
    maven {
        url "http://ci.shadowvolt.com/plugin/repository/everything/"
    }
}

dependencies {
    compile "com.tealcube.minecraft.bukkit:facecore-all:1.0.0"
    compile "com.tealcube.minecraft.bukkit:beast:1.0.0-SNAPSHOT"
    compile "com.tealcube.minecraft.bukkit:loot:1.0.0-SNAPSHOT"
    compile("com.tealcube.minecraft.bukkit:bullion:1.0.0-SNAPSHOT") {
        exclude module: "VaultAPI"
    }
}

processResources {
    filter ReplaceTokens, tokens: [
            VERSION: version + "-" + (System.getenv("BUILD_NUMBER") ?: "0"),
            NAME   : System.getenv("JOB_NAME") ?: project.archivesBaseName
    ]
}

license {
    header file('HEADER')
    ignoreFailures false
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

publishing {
    publications {
        java(MavenPublication) {
            artifactId = "strife"
            from components.java
        }
    }
    repositories {
        maven {
            url "http://tealcube.com/nexus/content/repositories/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}/"
            name "TealCube"
            credentials {
                username System.getenv("nexusUsername") ?: nexusUsername
                password System.getenv("nexusPassword") ?: nexusPassword
            }
        }
    }
}
